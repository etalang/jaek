use curses
use timer

seed: int = 1

prng(): int {
    seed = (75 * seed + 74) % 65537
    return seed
}

min(a: int, b: int): int {
    if a < b { return a } else { return b }
}

record Point {
    x, y: int
}

record Snake {
    body: Point[]
    head: int
    dx, dy: int
}

move_snake(s: Snake) {
    tail_idx: int = (s.head + 1) % length(s.body)
    head: Point = s.body[s.head]
    tail: Point = s.body[tail_idx]
    putCharAt(' ', tail.x, tail.y)
    tail.x, tail.y = head.x + s.dx, head.y + s.dy
    putCharAt('X', tail.x, tail.y)
    s.head = tail_idx
}

grow_snake(s: Snake) {
    new_body: Point[length(s.body) + 1]

    head: Point = s.body[s.head]
    new_body[0] = Point(head.x + s.dx, head.y + s.dy)
    putCharAt('X', new_body[0].x, new_body[0].y)

    i: int = 1
    while i <= length(s.body) {
        j: int = (s.head + i) % length(s.body)
        new_body[i] = s.body[j]
        i = i + 1
    }

    s.body = new_body
    s.head = 0
}

check_dead(s: Snake): bool {
    head: Point = s.body[s.head]
    i: int = 0
    while i < length(s.body) {
        if s.head != i & head.x == s.body[i].x & head.y == s.body[i].y {
            return true
        }
        i = i + 1
    }
    return false
}

main(args: int[][]) {
    food: Point = Point(7, 5)
    snake: Snake = Snake({Point(2, 2), Point(3, 2), Point(4, 2)}, 2, 1, 0)
    alive: bool = true

    initCurses()
    showCursor(false)
    echoKeys(false)
    inputTimeout(0)

    width: int, height: int = getWindowSize()
    width = min(width, 50)
    height = min(height, 20)

    putCharAt('x', food.x, food.y)
    putStringAt("XXX", snake.body[0].x, snake.body[0].y)

    time: int[] = getTimestamp()

    while alive {
        input: int = getInputChar()
        if snake.dy == 0 {
            if input == KEYUP()
                snake.dx, snake.dy = 0, -1
            else if input == KEYDOWN()
                snake.dx, snake.dy = 0, 1
        } else {
            if input == KEYLEFT()
                snake.dx, snake.dy = -1, 0
            else if input == KEYRIGHT()
                snake.dx, snake.dy = 1, 0
        }

        head: Point = snake.body[snake.head]

        if food.x == head.x & food.y == head.y {
            food.x, food.y = prng() % width, prng() % height
            putCharAt('x', food.x, food.y)
            grow_snake(snake)
        } else {
            putCharAt('x', food.x, food.y)
            move_snake(snake)
        }

        if check_dead(snake)
            alive = false

        refreshWindow()

        while timestampDifference(getTimestamp(), time) < 100 {}
        time = getTimestamp()
    }

    destroyCurses()
}
